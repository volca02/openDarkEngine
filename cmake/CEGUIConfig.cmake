#
# Try to find Cegui library on both Windows and Linux systems
# 
# The following values will get defined:
#	CEGUI_FOUND - True if cegui lib was found
#	CEGUI_INCLUDE_DIR - Include libraries for cegui usage
#	CEGUI_LIBRARIES - Library paths for cegui
#
# Please set your environmental variables to include CEGUI_HOME, which should point to the CEGUI SDK
#
SET(CEGUI_FOUND "No")

IF (UNIX)
    SET(CEGUI_INC_SEARCH_PATH
	/usr/include/cegui
	/usr/include/CEGUI
	/usr/include/OGRE
	/usr/local/include/cegui
	/usr/local/include/CEGUI
     )
     
     SET(CEGUI_LIB_SEARCH_PATH
        /lib
        /usr/lib
	/usr/lib32
	/usr/lib64
	/usr/local/lib
	/usr/local/lib32
	/usr/local/lib64
     )
 
	SET(CEGUIBASE_LIBNAMES CEGUIBase)
	SET(CEGUI_LIBNAMES CEGUIOgreRenderer OgreGUIRenderer)
ENDIF(UNIX)


IF(WIN32)
    # For the OgreSDK should be the prefared place to get the CEGUI files
    # The samples line is there to help building with the standard setup (Missing OgreCEGUIRenderer.h)

    SET(CEGUI_INC_SEARCH_PATH
	$ENV{OGRE_HOME}/include
	$ENV{OGRE_HOME}/include/CEGUI
	$ENV{OGRE_HOME}/samples/include
	$ENV{CEGUI_HOME}/include
	$ENV{CEGUI_HOME}/include/CEGUI
	$ENV{CEGUI_EXTRA_INCLUDE_DIR}
	$ENV{CEGUI_EXTRA_INCLUDE_DIR}/include
     )
     
     SET(CEGUI_LIB_SEARCH_PATH
	$ENV{OGRE_HOME}/lib
	$ENV{OGRE_HOME}/bin
	$ENV{OGRE_HOME}/bin/debug
	$ENV{OGRE_HOME}/bin/release
	$ENV{CEGUI_HOME}/lib
	$ENV{CEGUI_HOME}/bin
	$ENV{CEGUI_HOME}/bin/debug
	$ENV{CEGUI_HOME}/bin/release
	/OgreSDK/lib
	/OgreSDK/bin
	/OgreSDK/bin/debug
	/OgreSDK/bin/release
     )

	IF(MSVC)
		SET(CEGUI_LIBNAMES_DEB CEGUIOgreRenderer_d OgreGUIRenderer_d)	
		SET(CEGUI_LIBNAMES_REL CEGUIOgreRenderer OgreGUIRenderer)
		SET(CEGUIBASE_LIBNAMES_DEB CEGUIBase_d)	
		SET(CEGUIBASE_LIBNAMES_REL CEGUIBase)	
	ELSE(MSVC)
		SET(CEGUI_LIBNAMES CEGUIOgreRenderer OgreGUIRenderer)
		SET(CEGUIBASE_LIBNAMES CEGUIBase)	
	ENDIF(MSVC)
ENDIF(WIN32)

SET(CEGUI_ALL_FOUND 1)

# Include paths search
FIND_PATH(CEGUI_INCLUDE_DIR CEGUI.h ${CEGUI_INC_SEARCH_PATH})

IF(NOT CEGUI_INCLUDE_DIR)
	MESSAGE("WARNING: CEGUI.h not found under ${CEGUI_INC_SEARCH_PATH}")
	SET(CEGUI_ALL_FOUND 0)
ENDIF(NOT CEGUI_INCLUDE_DIR)

# Include paths search, renderer
FIND_PATH(CEGUI_EXTRA_INCLUDE_DIR OgreCEGUIRenderer.h ${CEGUI_INC_SEARCH_PATH})

IF(NOT CEGUI_EXTRA_INCLUDE_DIR)
	MESSAGE("WARNING: OgreCEGUIRenderer.h not found under ${CEGUI_INC_SEARCH_PATH}")
	SET(CEGUI_ALL_FOUND 0)
ENDIF(NOT CEGUI_EXTRA_INCLUDE_DIR)

# Cegui ogre renderer library
# Target compiler specific, as MSVC holds Debug and release separate
IF(MSVC)
	# Cegui library
	FIND_LIBRARY(CEGUI_CEGUI_LIB_RELEASE NAME ${CEGUIBASE_LIBNAMES_REL} PATHS ${CEGUI_LIB_SEARCH_PATH})
	FIND_LIBRARY(CEGUI_CEGUI_LIB_DEBUG NAME ${CEGUIBASE_LIBNAMES_DEB} PATHS ${CEGUI_LIB_SEARCH_PATH})

	IF(CEGUI_CEGUI_LIB_DEBUG AND CEGUI_CEGUI_LIB_RELEASE)
		SET(CEGUI_CEGUI_LIB optimized ${CEGUI_CEGUI_LIB_RELEASE} debug ${CEGUI_CEGUI_LIB_DEBUG})
	ELSE(CEGUI_CEGUI_LIB_DEBUG AND CEGUI_CEGUI_LIB_RELEASE)
		MESSAGE("WARNING: CEGUI library CEGUIBase not found under ${CEGUI_LIB_SEARCH_PATH}")
		SET(CEGUI_ALL_FOUND 0)
	ENDIF(CEGUI_CEGUI_LIB_DEBUG AND CEGUI_CEGUI_LIB_RELEASE)

	FIND_LIBRARY(CEGUI_OGRE_LIB_RELEASE ${CEGUI_LIBNAMES_REL} PATHS ${CEGUI_LIB_SEARCH_PATH})
	FIND_LIBRARY(CEGUI_OGRE_LIB_DEBUG ${CEGUI_LIBNAMES_DEB} PATHS ${CEGUI_LIB_SEARCH_PATH})

	IF(CEGUI_OGRE_LIB_DEBUG AND CEGUI_OGRE_LIB_RELEASE)
		SET(CEGUI_OGRE_LIB optimized ${CEGUI_OGRE_LIB_RELEASE} debug ${CEGUI_OGRE_LIB_DEBUG})
	ELSE(CEGUI_OGRE_LIB_DEBUG AND CEGUI_OGRE_LIB_RELEASE)
		MESSAGE("WARNING: CEGUI OGRE library CEGUIOgreRenderer not found under ${CEGUI_LIB_SEARCH_PATH}")
		SET(CEGUI_ALL_FOUND 0)
	ENDIF(CEGUI_OGRE_LIB_DEBUG AND CEGUI_OGRE_LIB_RELEASE)
ELSE(MSVC)
	# Cegui library
	FIND_LIBRARY(CEGUI_CEGUI_LIB NAME ${CEGUIBASE_LIBNAMES} PATHS ${CEGUI_LIB_SEARCH_PATH})

	IF(NOT CEGUI_CEGUI_LIB)
		MESSAGE("WARNING: CEGUI library CEGUIBase not found under ${CEGUI_LIB_SEARCH_PATH}")
		SET(CEGUI_ALL_FOUND 0)
	ENDIF(NOT CEGUI_CEGUI_LIB)


	FIND_LIBRARY(CEGUI_OGRE_LIB ${CEGUI_LIBNAMES} PATHS ${CEGUI_LIB_SEARCH_PATH})

	IF(NOT CEGUI_OGRE_LIB)
		MESSAGE("WARNING: CEGUI OGRE library CEGUIOgreRenderer not found under ${CEGUI_LIB_SEARCH_PATH}")
		SET(CEGUI_ALL_FOUND 0)
	ENDIF(NOT CEGUI_OGRE_LIB)
ENDIF(MSVC)

# Extra paths for include of CEGUI (for CEGUIOgreRenderer.h)
IF(CEGUI_EXTRA_INCLUDE_DIR)
    SET(CEGUI_INCLUDE_DIR ${CEGUI_EXTRA_INCLUDE_DIR} ${CEGUI_INCLUDE_DIR})
ENDIF(CEGUI_EXTRA_INCLUDE_DIR)

SET(CEGUI_LIBRARIES ${CEGUI_CEGUI_LIB} ${CEGUI_OGRE_LIB})

IF(CEGUI_INCLUDE_DIR AND CEGUI_CEGUI_LIB AND CEGUI_OGRE_LIB AND CEGUI_ALL_FOUND)
    SET(CEGUI_FOUND "Yes")
ENDIF(CEGUI_INCLUDE_DIR AND CEGUI_CEGUI_LIB AND CEGUI_OGRE_LIB AND CEGUI_ALL_FOUND)
