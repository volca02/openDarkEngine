; The script to gracefully install openDarkEngine under Microsoft Windows.
; 
; This file is part of openDarkEngine project
; Copyright (C) 2005-2006 openDarkEngine team
; 
; This program is free software; you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation; either version 2 of the License, or
; (at your option) any later version.
; 
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
; 
; You should have received a copy of the GNU General Public License
; along with this program; if not, write to the Free Software
; Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA 
;
; $Id$
;
;--------------------------------

SetCompressor /SOLID /FINAL lzma

!include nsDialogs.nsh
!insertmacro NSD_FUNCTION_INIFILE

ShowInstDetails show
ShowUninstDetails show
XPStyle on
AddBrandingImage left 129

Name "openDarkEngine"
Caption "openDarkEngine"
BrandingText "openDarkEngine Team"
LicenseData @CMAKE_CURRENT_SOURCE_DIR@\copying
LicenseForceSelection checkbox "I accept the terms in the license agreement"

Var Thief1Dir
Var Thief2Dir
Var Shock2Dir

OutFile "opdeSetup.exe"

InstallDir C:\Games\openDarkEngine

InstallDirRegKey HKLM "Software\NSIS_opde" "Install_Dir"

;Request application privileges for Windows Vista
RequestExecutionLevel admin

LoadLanguageFile "${NSISDIR}\Contrib\Language files\English.nlf"

;--------------------------------

VIProductVersion "@OPDE_VER_MAJOR@.@OPDE_VER_MINOR@.@OPDE_VER_PATCH@.0"
VIAddVersionKey /LANG=${LANG_ENGLISH} "ProductName" "openDarkEngine"
VIAddVersionKey /LANG=${LANG_ENGLISH} "Comments" "El Motor Oscuro"
VIAddVersionKey /LANG=${LANG_ENGLISH} "CompanyName" "openDarkEngine Team"
VIAddVersionKey /LANG=${LANG_ENGLISH} "LegalTrademarks" "GPL"
VIAddVersionKey /LANG=${LANG_ENGLISH} "LegalCopyright" "© openDarkEngine Team"
VIAddVersionKey /LANG=${LANG_ENGLISH} "FileDescription" "openDarkEngine"
VIAddVersionKey /LANG=${LANG_ENGLISH} "FileVersion" "@OPDE_VER_MAJOR@.@OPDE_VER_MINOR@.@OPDE_VER_PATCH@"

;--------------------------------

Page Custom ShowWelcome
Page license
Page components
Page directory
Page instfiles
Page Custom SelectGames CreateResourceCfg

UninstPage uninstConfirm
UninstPage instfiles

;--------------------------------

Function .onInit
	System::Call 'kernel32::CreateMutexA(i 0, i 0, t "myMutex") i .r1 ?e'
		Pop $R0

	StrCmp $R0 0 +3
		MessageBox MB_OK|MB_ICONEXCLAMATION "The openDarkEngine installer is already running."
		Abort
FunctionEnd

Function .onGUIInit
	InitPluginsDir
	File /oname=$PLUGINSDIR\installer.bmp @CMAKE_CURRENT_SOURCE_DIR@\installer\installer.bmp
	SetBrandingImage /RESIZETOFIT $PLUGINSDIR\installer.bmp
FunctionEnd

Function un.onInit
   	System::Call 'kernel32::CreateMutexA(i 0, i 0, t "myMutex") i .r1 ?e'
   		Pop $R0
    
    StrCmp $R0 0 +3
    	MessageBox MB_OK|MB_ICONEXCLAMATION "The openDarkEngine uninstaller is already running."
    	Abort
FunctionEnd

Function un.onGUIInit
	InitPluginsDir
	File /oname=$PLUGINSDIR\installer.bmp @CMAKE_CURRENT_SOURCE_DIR@\installer\installer.bmp
	SetBrandingImage /RESIZETOFIT $PLUGINSDIR\installer.bmp
FunctionEnd

Function ShowWelcome
	nsDialogs::Create /NOUNLOAD 1018
	Pop $0

	${NSD_CreateLabel} 10 10u 100% 80u "Welcome to openDarkEngine installation."
	Pop $0

	nsDialogs::Show
FunctionEnd

Function SelectGames	
	File /oname=$PLUGINSDIR\installer.ini "@CMAKE_CURRENT_SOURCE_DIR@\installer\installer.ini"

	StrCpy $0 "$PLUGINSDIR\installer.ini"

	Call CreateDialogFromINI
FunctionEnd

Function CreateResourceCfg
	Call UpdateINIState
	
	ReadINIStr $Thief1Dir "$PLUGINSDIR\installer.ini" "Field 3" "State"
	ReadINIStr $Thief2Dir "$PLUGINSDIR\installer.ini" "Field 5" "State"
	ReadINIStr $Shock2Dir "$PLUGINSDIR\installer.ini" "Field 7" "State"  
	
	Call SetupThief1
	Call SetupThief2
	Call SetupShock2	

FunctionEnd

Function SetupThief1
	FileOpen $0 $INSTDIR\thief1.cfg w			
	FileWrite $0 "[General]$\r$\nFileSystem=.\$\r$\nFileSystem=.\scripts\$\r$\n"	
	FileWrite $0 "$\r$\n[Thief1]$\r$\n"
	FileWrite $0 "FileSystem=.\scripts\Thief1$\r$\nFileSystem=$Thief1Dir$\r$\n"
	
	StrCmp $Thief1Dir "" empty	
	FindFirst $2 $1 "$Thief1Dir\crfs\*.crf"
	loop:
	  StrCmp $1 "" done
	  FileWrite $0 "Zip=$Thief1Dir\crfs\$1$\r$\n"
	  FindNext $2 $1
	  Goto loop
	done:
	FindClose $2
	empty:
	FileClose $0

FunctionEnd

Function SetupThief2
	FileOpen $0 $INSTDIR\thief2.cfg w			
	FileWrite $0 "[General]$\r$\nFileSystem=.\$\r$\nFileSystem=.\scripts\$\r$\n"
	FileWrite $0 "$\r$\n[Thief2]$\r$\n"
	FileWrite $0 "FileSystem=.\scripts\Thief2$\r$\nFileSystem=$Thief2Dir$\r$\n"
	
	StrCmp $Thief2Dir "" empty	
	FindFirst $2 $1 "$Thief2Dir\res\*.crf"
	loop:
	  StrCmp $1 "" done
	  FileWrite $0 "Zip=$Thief2Dir\res\$1$\r$\n"
	  FindNext $2 $1
	  Goto loop
	done:
	FindClose $2
	empty:
	FileClose $0

FunctionEnd

Function SetupShock2
	FileOpen $0 $INSTDIR\shock2.cfg w			
	FileWrite $0 "[General]$\r$\nFileSystem=.\$\r$\nFileSystem=.\scripts\$\r$\n"
	FileWrite $0 "$\r$\n[Shock2]$\r$\n"
	FileWrite $0 "FileSystem=.\scripts\Shock2$\r$\nFileSystem=$Shock2Dir$\r$\n"
	
	StrCmp $Shock2Dir "" empty	
	FindFirst $2 $1 "$Shock2Dir\*.crf"
	loop:
	  StrCmp $1 "" done
	  FileWrite $0 "Zip=$Shock2Dir\$1$\r$\n"
	  FindNext $2 $1
	  Goto loop
	done:
	FindClose $2
	empty:
	FileClose $0

FunctionEnd

;--------------------------------

Section "openDarkEngine (required)"	

	SectionIn RO

	SetOutPath $INSTDIR
	File @PROJECT_BINARY_DIR@\src\main\release\opde.exe
	File /oname=opde.cfg @CMAKE_CURRENT_SOURCE_DIR@\installer\opde-sample.cfg
	File "$%OGRE_HOME%\bin\release\*.dll"	
	File "$%OGRE_HOME%\bin\release\Plugins.cfg"
	File "@FREEIMAGE_PATH@\Dist\FreeImage.dll"
	FILE /x py*_d.dll "$%WinDir%\System32\Py*.dll"
	
	SetOutPath $INSTDIR\Scripts
	File /r /x .svn "@CMAKE_CURRENT_SOURCE_DIR@\Scripts\*.*"
	

	WriteRegStr HKLM SOFTWARE\NSIS_opde "Install_Dir" "$INSTDIR"

	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\opde" "DisplayName" "NSIS opde"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\opde" "UninstallString" '"$INSTDIR\uninstall.exe"'
	WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\opde" "NoModify" 1
	WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\opde" "NoRepair" 1
	WriteUninstaller "uninstall.exe"
  
SectionEnd

Section "Start Menu Shortcuts"

	SetOutPath $INSTDIR
	CreateDirectory "$SMPROGRAMS\openDarkEngine"
	CreateShortCut "$SMPROGRAMS\openDarkEngine\openDarkEngine (TDP, TG).lnk" "$INSTDIR\opde.exe" "T1" "$INSTDIR\opde.exe" 0
	CreateShortCut "$SMPROGRAMS\openDarkEngine\openDarkEngine (TMA).lnk" "$INSTDIR\opde.exe" "T2" "$INSTDIR\opde.exe" 0
	CreateShortCut "$SMPROGRAMS\openDarkEngine\openDarkEngine (SS2).lnk" "$INSTDIR\opde.exe" "SS2" "$INSTDIR\opde.exe" 0
	CreateShortCut "$SMPROGRAMS\openDarkEngine\Uninstall.lnk" "$INSTDIR\uninstall.exe" "" "$INSTDIR\uninstall.exe" 0
  
SectionEnd

;--------------------------------

; Uninstaller

Section "Uninstall"
  
	; Remove registry keys
	DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\opde"
	DeleteRegKey HKLM SOFTWARE\NSIS_opde

	; Remove files and uninstaller
	Delete $INSTDIR\opde.exe
	Delete $INSTDIR\*.cfg
	Delete $INSTDIR\*.log
	Delete $INSTDIR\*.dll
	Delete $INSTDIR\*.bmp
	Delete $INSTDIR\uninstall.exe

	; Remove shortcuts, if any
	Delete "$SMPROGRAMS\openDarkEngine\*.*"

	; Remove directories used
	RMDir "$SMPROGRAMS\openDarkEngine"
	RMDir /r "$INSTDIR\Scripts\"
	RMDir "$INSTDIR"


SectionEnd